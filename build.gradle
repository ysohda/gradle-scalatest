
plugins {
    id 'com.gradle.plugin-publish' version '1.0.0'
}

apply plugin: 'signing'
apply plugin: 'groovy'

ext {
    websiteUrl = 'https://github.com/ysohda/gradle-scalatest'
    sourceUrl = 'https://github.com/ysohda/gradle-scalatest.git'
}

targetCompatibility = '1.8'

repositories {
    mavenCentral()
}

dependencies {
    implementation gradleApi()
    implementation localGroovy()
    testImplementation 'junit:junit:4.12'
}

task groovydocJar(type: Jar, dependsOn: groovydoc) {
    from "$buildDir/docs/groovydoc"
    archiveClassifier = 'groovydoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    archiveClassifier = 'sources'
}

test.dependsOn(jar)

test {
    testLogging.showStandardStreams = System.env.CI == 'true'
}

task showVersion() { doLast { println(project.version) } }

group = 'io.github.ysohda.scalatest'
description = 'gradle-scalatest is a Gradle plugin for executing scalatest tests'
version '0.30.1'
if (project.version == 'unspecified') {
    version = '0.1-SNAPSHOT'
}

gradlePlugin {
    plugins {
        scalatestPlugin {
            id = 'io.github.ysohda.scalatest'
            displayName = 'Gradle ScalaTest plugin'
            description = project.description
            implementationClass = 'com.github.ysohda.ScalaTestPlugin'
        }
    }
}

pluginBundle {
    website = project.websiteUrl
    vcsUrl = project.sourceUrl
    tags = ['testing', 'scala', 'scalatest']
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'io.github.ysohda'
            artifactId = 'scalatest'
            version = version
            pom {
                name = 'com.github.ysohda.scalatest.gradle.plugin'
                description = project.description
                url = project.websiteUrl
                scm {
                    url = "scm:git:${project.sourceUrl}"
                }

                licenses {
                    license {
                        name = 'unlicense'
                        url = 'http://unlicense.org'
                        distribution = 'repo'
                    }
                }

                developers {
                    developer {
                        id = 'maiflai'
                    }
                    developer {
                        id = 'ysohda'
                    }
                }
            }
            from components.java
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}
